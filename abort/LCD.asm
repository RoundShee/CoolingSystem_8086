;经过调整,暂时将8255的端口放置在230H处

DATA	SEGMENT
I8255A	EQU	230H
I8255B	EQU	231H
I8255C	EQU	232H
I8255K	EQU	233H
HZ_TAB	DB	'Cooling System  '
		DB	'@RoundShee      '
HZ_ADR	DB	?
DATA	ENDS


STA		SEGMENT		STACK
		DB	256		DUP(0)
STA		ENDS


CODE	SEGMENT
	ASSUME CS:CODE,DS:DATA,SS:STA
START:	MOV		AX,DATA
		MOV		DS,AX
		MOV		DX,I8255K
		MOV		AL,10000010B
		OUT 	DX,AL
		CALL	LCD_INIT
		LEA		BX,HZ_TAB
		MOV		CH,2
		CALL	LCD_DISP
		LEA		BX,HZ_TAB
		MOV		CH,3
		CALL	LCD_DISP
LL:		JMP		LL

;使用AX、DX寄存器-LCD清平设置无光标无反白
LCD_INIT PROC
		CALL	CHECK_BUSY			;先检查状态再写数据
		MOV		AL,01H				;清屏命令字
		MOV		DX,I8255A
		OUT		DX,AL				;设置CLEAR命令
		CALL	CMD_SETUP			;启动LCD执行程序
		CALL	CHECK_BUSY
		MOV		AL,0CH				;显示状态开关，开显示，无光标，无反白
		MOV		DX,I8255A
		OUT		DX,AL				;设置CLEAR命令
		CALL	CMD_SETUP			;启动LCD执行命令
		RET
LCD_INIT ENDP

;显示HZ_TAB
LCD_DISP PROC
		LEA		BX,HZ_TAB
		CMP		CH,2
		JZ		DISP_SEC
		MOV		BYTE PTR HZ_ADR,98H	;第三行起始端口地址
		ADD		BX,16				;指向第二行信息
		JMP		NEXT
DISP_SEC: MOV	BYTE PTR HZ_ADR,90H
NEXT:	MOV		CL,8
CONTINUE: PUSH	CX
		CALL	CHECK_BUSY
		MOV		AL,HZ_ADR
		MOV		DX,I8255A
		OUT		DX,AL
		CALL	CMD_SETUP			;设定DDRAM地址命令

		CALL	CHECK_BUSY
		MOV		AL,[BX]				;先送汉字编码高位

		MOV		DX,I8255A
		OUT		DX,AL
		CALL	DATA_SETUP			;输出汉字编码高字节

		CALL	CHECK_BUSY
		INC		BX					;修改显示内码缓冲区指针
		MOV		DX,I8255A
		MOV		AL,[BX]
		OUT		DX,AL
		CALL	DATA_SETUP			;输出汉字编码低字节

		CALL	CHECK_BUSY
		INC		BX
		INC		BYTE PTR HZ_ADR		;修改LCD显示端口地址
		POP		CX
		DEC		CL
		JNZ		CONTINUE
		RET
LCD_DISP ENDP

;将8255A口改输入读取高位状态并等待不忙
CHECK_BUSY PROC
		MOV		DX,I8255K
		MOV		AL,10010000B		;PA输入
		OUT		DX,AL
		MOV		DX,I8255C			;指向8255端口控制端口
		MOV		AL,00000000B		;PC1置0，PC0置0(LCD I端=0, W端=0)
		OUT		DX,AL
		MOV		DX,I8255C			;指向8255端口控制端口
		MOV		AL,00000010B		;PC1置0，PC0置0(LCD I端=0, W端=0)
		OUT		DX,AL
		NOP
		MOV		AL,00000110B		;PC2置1(LCD E端=1)
		OUT		DX,AL
		NOP
		MOV		DX,I8255A
BUSYLOP: IN		AL,DX
		TEST	AL,80H
		JNZ		BUSYLOP
		MOV		DX,I8255C			;指向8255端口控制端口
		MOV		AL,00000000B		;PC1置0，PC0置0(LCD I端=0, W端=0)
		OUT		DX,AL
		NOP
		MOV		DX,I8255K
		MOV		AL,10000000B		;PA输出
		OUT		DX,AL
		RET
CHECK_BUSY ENDP

;命令时序操控
CMD_SETUP PROC
		MOV		DX, I8255C			;指向8255端口控制端口
		MOV		AL, 00000000B		;PC1置0，PCO置0 (LCD I端=0，W端=0)
		OUT		DX, AL
		NOP
		MOV		AL, 00000100B		;PC2置1 (LCD E端=1)
		OUT		DX, AL
		NOP
		MOV 	DX, I8255C
		MOV		AL, 00000000B		;PC2置0，(LCD E端置0)
		OUT		DX, AL
		RET
CMD_SETUP ENDP

;写入数据的时序操作
DATA_SETUP PROC 
		MOV		DX, I8255C			;指向8255控制端口
		MOV 	AL, 00000001B 		;PC1置0，PC0=1 (LCD I端=1)
		OUT		DX, AL
		NOP
		CALL	DELAY
		MOV		AL, 00000101B		;PC2置1 (LCD E端=1)
		OUT		DX, AL
		CALL	DELAY
		NOP
		NOP
		MOV		AL, 00000001B		;PC2置0，(LCD E端=0)
		OUT		DX, AL
		NOP
		MOV		DX, I8255C
		MOV		AL, 00000000B		;PC2置0, (LCD E端置0)
		OUT		DX, AL
		RET
DATA_SETUP ENDP

;延迟,具有保护现场功能
DELAY	PROC
		PUSH	CX
		PUSH	DX
		MOV		CX, 00FFH
X1:		LOOP	X1
		POP		DX
		POP		CX
		RET
DELAY	ENDP
CODE	ENDS
		END		START
		