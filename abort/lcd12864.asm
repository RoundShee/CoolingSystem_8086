; 128*64字符LCD点阵显示模块电路
; 独立  并行模式    P74时序图   P119电路连接 
; LCD--8255并行接口  PA接LCD_DB PC2--EN PC1--WR  PC0--DI
;  CPU--8255 CS用230H
; 代码解析在P120
DATA	SEGMENT
I8255A		EQU		230H
I8255B		EQU		231H
I8255C		EQU		232H
I8255K		EQU		233H
HZ_ONE		DB		'  散热系统  '	;第一行
HZ_TWO		DB		'  自动模式  '	;
HZ_THR		DB		'温度:27     '	;
HZ_FOU		DB		'转速:2     '	;
HZ_ADR		DB		?				;存放显示行起始端口地址
DATA	ENDS


STA		SEGMENT		STACK
			DB		265 DUP(0)
STA		ENDS


CODE	SEGMENT
		ASSUME	CS:CODE,DS:DATA,SS:STA
MAIN	PROC	FAR
		MOV		AX,DATA
		MOV		DS,AX
		MOV		DX,I8255K			;这三行都是
		MOV		AL,10000010B		;设置为
		OUT 	DX,AL				;PA输出	PB输入	PC输出
		CALL	LCD_INIT
		LEA		BX,HZ_ONE
		MOV		CH,1
		CALL	LCD_DISP
		LEA		BX,HZ_TWO
		MOV		CH,2
		CALL	LCD_DISP
		LEA		BX,HZ_THR
		MOV		CH,3
		CALL	LCD_DISP
		LEA		BX,HZ_FOU
		MOV		CH,4
		CALL	LCD_DISP
LL:		JMP		LL
MAIN	ENDP

;8255初始化
;使用AX、DX寄存器-LCD清平设置无光标无反白
LCD_INIT PROC	FAR
		CALL	CHECK_BUSY			;先检查状态再写数据
		MOV		AL,01H				;清屏命令字
		MOV		DX,I8255A
		OUT		DX,AL				;设置CLEAR命令
		CALL	CMD_SETUP			;启动LCD执行程序
		CALL	CHECK_BUSY
		MOV		AL,0CH				;显示状态开关，开显示，无光标，无反白
		MOV		DX,I8255A
		OUT		DX,AL				;设置CLEAR命令
		CALL	CMD_SETUP			;启动LCD执行命令
		RET
LCD_INIT ENDP

;显示BX所☞内容，CH第几行
LCD_DISP PROC	FAR
		CMP		CH,1
		JZ		GIVE_ONE
		CMP		CH,2
		JZ		GIVE_TWO
		CMP		CH,3
		JZ		GIVE_THR
		CMP		CH,4
		JZ		GIVE_FOU
GIVE_ONE:
		MOV		BYTE PTR HZ_ADR,80H	;指向第一行
		JMP		NEXT
GIVE_TWO:
		MOV		BYTE PTR HZ_ADR,90H	;指向第二行
		JMP		NEXT
GIVE_THR:
		MOV		BYTE PTR HZ_ADR,88H	;指向第三行
		JMP		NEXT
GIVE_FOU:
		MOV		BYTE PTR HZ_ADR,98H	;指向第四行
		JMP		NEXT
NEXT:	MOV		CL,8
CONTINUE: PUSH	CX					;保护循环变量
		CALL	CHECK_BUSY
		MOV		AL,HZ_ADR
		MOV		DX,I8255A
		OUT		DX,AL
		CALL	CMD_SETUP			;设定DDRAM地址命令

		CALL	CHECK_BUSY
		MOV		AL,[BX]				;先送汉字编码高位

		MOV		DX,I8255A
		OUT		DX,AL
		CALL	DATA_SETUP			;输出汉字编码高字节

		CALL	CHECK_BUSY
		INC		BX					;修改显示内码缓冲区指针
		MOV		DX,I8255A
		MOV		AL,[BX]
		OUT		DX,AL
		CALL	DATA_SETUP			;输出汉字编码低字节

		CALL	CHECK_BUSY
		INC		BX
		INC		BYTE PTR HZ_ADR		;修改LCD显示端口地址
		POP		CX
		DEC		CL
		JNZ		CONTINUE
		RET
LCD_DISP ENDP

;将8255A口改输入读取高位状态并等待不忙
CHECK_BUSY PROC		FAR
		MOV		DX,I8255K
		MOV		AL,10010000B		;PA输入
		OUT		DX,AL
		MOV		DX,I8255C			;指向8255端口控制端口
		MOV		AL,00000000B		;PC1置0，PC0置0(LCD I端=0, W端=0)
		OUT		DX,AL
		MOV		DX,I8255C			;指向8255端口控制端口
		MOV		AL,00000010B		;PC1置0，PC0置0(LCD I端=0, W端=0)
		OUT		DX,AL
		NOP
		MOV		AL,00000110B		;PC2置1(LCD E端=1)
		OUT		DX,AL
		NOP
		MOV		DX,I8255A
BUSYLOP: IN		AL,DX
		TEST	AL,80H
		JNZ		BUSYLOP
		MOV		DX,I8255C			;指向8255端口控制端口
		MOV		AL,00000000B		;PC1置0，PC0置0(LCD I端=0, W端=0)
		OUT		DX,AL
		NOP
		MOV		DX,I8255K
		MOV		AL,10000000B		;PA输出
		OUT		DX,AL
		RET
CHECK_BUSY ENDP

;命令时序操控
CMD_SETUP PROC		FAR
		MOV		DX, I8255C			;指向8255端口控制端口
		MOV		AL, 00000000B		;PC1置0，PCO置0 (LCD I端=0，W端=0)
		OUT		DX, AL
		NOP
		MOV		AL, 00000100B		;PC2置1 (LCD E端=1)
		OUT		DX, AL
		NOP
		MOV 	DX, I8255C
		MOV		AL, 00000000B		;PC2置0，(LCD E端置0)
		OUT		DX, AL
		RET
CMD_SETUP ENDP

;写入数据的时序操作
DATA_SETUP PROC 	FAR
		MOV		DX, I8255C			;指向8255控制端口
		MOV 	AL, 00000001B 		;PC1置0，PC0=1 (LCD I端=1)
		OUT		DX, AL
		NOP
		CALL	DELAY
		MOV		AL, 00000101B		;PC2置1 (LCD E端=1)
		OUT		DX, AL
		CALL	DELAY
		NOP
		NOP
		MOV		AL, 00000001B		;PC2置0，(LCD E端=0)
		OUT		DX, AL
		NOP
		MOV		DX, I8255C
		MOV		AL, 00000000B		;PC2置0, (LCD E端置0)
		OUT		DX, AL
		RET
DATA_SETUP ENDP

;延迟,具有保护现场功能
DELAY	PROC	FAR
		PUSH	CX
		PUSH	DX
		MOV		CX, 00FFH
X1:		LOOP	X1
		POP		DX
		POP		CX
		RET
DELAY	ENDP

CODE	ENDS

		END		MAIN
			